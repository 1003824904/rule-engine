
import com.cubbery.salary.Salary;
import com.cubbery.salary.Position;


dialect  "java"

//参数初始化
rule "salary-init"
    when

    then
end

//计算基本工资
rule "salary-base-internship"
    salience 500
    activation-group "salary-base"
    when
        $salary : Salary(position == Position.internship)
    then
        $salary.setBase(500);
end

rule "salary-base-common"
    salience 500
    activation-group "salary-base"
    when
        $salary : Salary(position == Position.common)
    then
        $salary.setBase(1000);
end

rule "salary-base-manager"
    salience 500
    activation-group "salary-base"
    when
        $salary : Salary(position == Position.manager)
    then
        $salary.setBase(2000);
end

rule "salary-base-chief"
    salience 500
    activation-group "salary-base"
    when
        $salary : Salary(position == Position.chief)
    then
        $salary.setBase(5000);
end


//计算奖金
rule "salary-bonus1"
    salience 404
    activation-group "salary-bonus"
    when
        $salary : Salary(achievement <= 20000)
    then
        $salary.setBonus(1000);
end
rule "salary-bonus2"
    salience 403
    activation-group "salary-bonus"
    when
        $salary : Salary(achievement <= 50000 )
    then
        $salary.setBonus(2000);
end
rule "salary-bonus3"
    salience 402
    activation-group "salary-bonus"
    when
        $salary : Salary(achievement > 50000)
    then
        $salary.setBonus(5000);
end



//计算税前
rule "salary-pre"
    salience 300
    when
        $salary : Salary()
    then
        $salary.setPreTax($salary.getBase() + $salary.getBonus());
end

//计算所得税
rule "salary-incomeTax1"
    salience 204
    activation-group "salary-incomeTax"
    when
        $salary : Salary(preTax <= 1800.0)
    then
        $salary.setIncomeTax(35.0);
        System.out.println("--11--");
end
rule "salary-incomeTax3"
    salience 202
    activation-group "salary-incomeTax"
    when
        $salary : Salary(preTax < 8000.0)
    then
        $salary.setIncomeTax(0.1 * ($salary.getPreTax() - 1800.0) + 35);
        System.out.println("--22--");
end
rule "salary-incomeTax4"
    salience 201
    activation-group "salary-incomeTax"
    when
        $salary : Salary()
    then
        $salary.setIncomeTax(0.15 * ($salary.getPreTax() - 8000) + 135);
        System.out.println("--33--");
end



//计算税后
rule "salary-after"
    salience 100
    when
        $salary : Salary()
    then
        $salary.setAfterTax($salary.getPreTax() - $salary.getIncomeTax());
end
